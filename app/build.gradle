import groovy.json.JsonSlurper

apply plugin: 'checkstyle'
apply plugin: 'com.android.application'
apply plugin: 'io.freefair.android-lombok'
apply plugin: 'pmd'

def versionJson = new JsonSlurper().parseText(rootProject.layout.projectDirectory.file('version.json').asFile.text)

android {
    compileSdk 35
    // NONE: 0, VERBOSE: 2, DEBUG: 3, INFO: 4, WARN: 5, ERROR: 6, ASSERT: 7
    final def logLevelDefault = "2"
    final def logLevelApk = "0"
    final def logLevelRelease = "0"
    final def logLevelVm = "0"

    defaultConfig {
        applicationId "com.rdapps.gamepad"
        minSdkVersion 28
        targetSdkVersion 34
        versionCode versionJson.versionCode
        versionName versionJson.version
        resValue "string", "version_name", versionName
        buildConfigField  'boolean', 'CHECK_UPDATE', 'false'
        buildConfigField  'int', 'LOG_LEVEL', logLevelDefault
        buildConfigField  'byte[]', 'DEBUG_INPUT', '{0x21, 0x31, 0x30}'
        buildConfigField  'byte[]', 'DEBUG_OUTPUT', '{0x11, 0x01}'
        buildConfigField  'boolean', 'VM', 'false'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'VM', 'true'
        }
        apk {
            buildConfigField 'int', 'LOG_LEVEL', logLevelApk
            buildConfigField 'boolean', 'CHECK_UPDATE', 'true'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField 'int', 'LOG_LEVEL', logLevelRelease
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        vm {
            minifyEnabled true
            buildConfigField 'int', 'LOG_LEVEL', logLevelVm
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'VM', 'true'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    androidResources {
        noCompress = 'jcui'
    }
    namespace 'com.rdapps.gamepad'
    lint {
        disable 'VectorPath'
        abortOnError false
    }
    buildFeatures {
        buildConfig = true
    }
}

checkstyle {
    toolVersion = '10.23.1'
}

pmd {
    toolVersion = '6.55.0'
}

tasks.register('checkstyle', Checkstyle) {
    configFile rootProject.file('config/checkstyle/rules.xml')
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    configProperties = [
            "org.checkstyle.google.suppressionfilter.config":
                    rootProject.file('config/checkstyle/checkstyle-suppressions.xml')]
    classpath = files()
}

tasks.register('pmd', Pmd) {
    source 'src'
    include '** /*.java'
    exclude '** /gen/**'
    exclude '** /R.java'
    exclude '** /BuildConfig.java'
    ruleSets = ['category/java/errorprone.xml', 'category/java/bestpractices.xml']
    consoleOutput = true
}

preBuild.configure {
    dependsOn 'checkstyle' //, 'pmd'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.7.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'com.google.android.material:material:1.12.0'

    //Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.mockito:mockito-core:5.7.0'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    //Rest API
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'commons-io:commons-io:2.19.0'

    constraints {
        implementation('com.fasterxml.jackson.core:jackson-databind:2.19.0') {
            because '2.10.1 is vulnerable'
        }
        implementation('com.squareup.okhttp3:okhttp:4.12.0') {
            because '3.14.9 is vulnerable'
        }
    }

    //Color Picker
    implementation 'com.github.alexandre-g:AndroidPhotoshopColorPicker:1.2.4'

    //Device Name
    implementation 'com.jaredrummler:android-device-names:1.1.9'

}