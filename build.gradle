// Top-level build file where you can add configuration options common to all sub-projects/modules.

import groovy.json.JsonOutput
import org.ajoberstar.grgit.gradle.GrgitService

import javax.inject.Inject

buildscript {
    
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.10.1'
        classpath 'io.freefair.gradle:android-gradle-plugins:8.10.0'
    }
}

plugins {
    id 'org.ajoberstar.grgit-service' version '5.0.0-rc.3'
    id 'org.owasp.dependencycheck' version '12.1.3' apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url = 'https://jitpack.io' }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

class VersionJsonTask extends DefaultTask {
    private final Provider<GrgitService> service
    private final String version

    @OutputFile
    final abstract RegularFileProperty outputFile = project.objects.fileProperty()
            .convention(project.layout.projectDirectory.file('version.json'))

    @Inject
    VersionJsonTask(Provider<GrgitService> service, Project project) {
        this.service = service
        this.version = project.version
    }

    @TaskAction
    void execute() {
        outputFile.get().asFile.write(JsonOutput.prettyPrint(JsonOutput.toJson([
                version: version,
                versionCode: service.get().grgit.log().size()
        ])))
    }
}

tasks.register('generateVersionJson', VersionJsonTask, grgitService.service)

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}
